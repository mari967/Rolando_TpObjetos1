import Rolando.*
import Hechizos.*
import FuerzaOscura.*
import Artefactos.*

describe "Lucha y hechicería avanzada" {

	fixture {
		espectroMalefico.cambiarNombre("espectro malefico")
		rolando.hechizoPreferido(espectroMalefico)
		collarDivino.perlas(5)
		rolando.agregarArtefacto(espadaDelDestino)
		rolando.agregarArtefacto(collarDivino)
		rolando.agregarArtefacto(mascaraOscura)
		rolando.agregarArtefacto(armadura)
		rolando.agregarArtefacto(espejo)
	}
	test "Rolando está cargado" {
		assert.that(rolando.estaCargado())
	}
	test "Sin la armadura, Rolando no está cargado" {
		rolando.removerArtefacto(armadura)
		assert.notThat(rolando.estaCargado())
	}
	test "Con todos los artefactos, la habilidad de lucha de Rolando es 20" {
		assert.equals(20, rolando.habilidadParaLuchar())
	}
	test "Si la armadura está bendecida, sus unidades de lucha son 58" {
		armadura.refuerzo(bendicion)
		assert.equals(58, armadura.unidadesDeLucha())
	}
	test "Si la armadura tiene cota de malla, sus unidades de lucha son 3" {
		armadura.refuerzo(cotaDeMalla)
		assert.equals(3, armadura.unidadesDeLucha())
	}
	test "Si la armadura tiene un hechizo básico, sus unidades de lucha son 12" {
		armadura.refuerzo(hechizoBasico)
		assert.equals(12, armadura.unidadesDeLucha())
	}
	test "Si la armadura tiene un espectroMalefico, sus unidades de lucha son 19" {
		armadura.refuerzo(espectroMalefico)
		assert.equals(19, armadura.unidadesDeLucha())
	}
	test "Si Rolando solo tiene espejo, su habilidad de lucha es 1" {
		rolando.removerTodosLosArtefactos()
		assert.equals(1, rolando.habilidadParaLuchar())
	}
	test "Si Rolando tiene un libro de hechizos, con espectro maléfico 
		  y hechizo básico, como hechizo favorito, su nivel de hechicería es 56" {
		libroDeHechizos.agregarHechizo(hechizoBasico)
		libroDeHechizos.agregarHechizo(espectroMalefico)
		rolando.hechizoPreferido(libroDeHechizos)
		assert.equals(56, rolando.nivelDeHechiceria())
	}
}